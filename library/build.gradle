plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace = rootProject.ext.namespace
    compileSdk = rootProject.ext.targetSdkVersion
    ndkVersion = rootProject.ext.ndkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
    }

    externalNativeBuild {
        ndkBuild {
            path file("Application.mk")
        }
    }

    flavorDimensions 'cpuArch'

    productFlavors {
        arm64 {
            dimension 'cpuArch'
            ndk {
                abiFilter "arm64-v8a"
            }
            minSdkVersion rootProject.ext.minSdkVersion
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    lintOptions {
        abortOnError false
    }

    publishing {
        singleVariant("arm64Release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation rootProject.ext.websocket
    implementation rootProject.ext.conscrypt
    implementation rootProject.ext.constraintLayout
}

afterEvaluate {
    publishing {
        publications {
            create("release", MavenPublication) {
                from components.findByName("arm64Release")

                groupId = rootProject.ext.namespace
                artifactId = rootProject.ext.name
                version = rootProject.ext.versionName
            }
        }
    }
}

tasks.named("publishToMavenLocal").configure {
    dependsOn("assembleRelease")
}